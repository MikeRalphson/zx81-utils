   1 REM E#RND# RETURN,CB7Y#WyCPI7777#FF RETURN REM C##7#7;/ CONT 777#FFF RETURN,4 INPUT 777 GOSUB #(RND<<<<<<#  7Y,yC"LEN #yC##><#/ LOAD 6#RNDTAN F# COPY COPY/ PLOT 5#  GOSUB #MRND6MRND5#RND7#7#uC#ABS  UNPLOT RND7#7##/ LOAD #v AND  LIST RNDFF6#RNDTAN RNDTAN 
   2 DIM A$(32)
   3 LET RESTORE=16601
   4 LET READ=16514
   5 RAND 1
   6 RAND USR RESTORE
   7 FAST
   8 RAND 
   9 POKE 16389,67
  10 CLS
  11 PRINT TAB 10;"HITCH-HIKER"
  20 PRINT TAB 10;"###########"
  30 PRINT 
  50 DIM R$(8,8,9)
  60 FOR D=1 TO 8
  70 FOR A=1 TO 8
  80 LET R$(D,A)=A$(1 TO USR READ)+CHR$ INT (RND*10+38)
  90 NEXT A
 100 NEXT D
 105 LET N$="INT (RND*8+1)"
 110 FOR I=1 TO 3
 120 LET X$="PMD"(I)
 130 FOR J=1 TO 13+4*RND
 140 LET D=VAL N$
 150 LET A=VAL N$
 160 IF R$(D,A,6)<>" " THEN GOTO 140
 170 LET R$(D,A,6)=X$
 180 NEXT J
 190 NEXT I
 200 FOR I=1 TO 40
 210 LET D=VAL N$
 220 LET A=VAL N$
 230 IF R$(D,A,7)<>" " THEN GOTO 210
 240 LET R$(D,A,7)=CHR$ INT (RND*20+38)
 250 NEXT I
 260 LET R$(VAL N$,VAL N$,5)="K"
 262 LET D=VAL N$
 265 LET A=VAL N$
 267 IF R$(D,A,5)="K" THEN GOTO 262
 270 LET R$(D,A,5)="L"
 280 LET M=5
 290 LET S=0
 300 LET E=200
 310 LET BP=20
 320 LET AD=0
 330 LET T$="2"
 340 LET W$="LASER PISTOL"
 350 LET MD=4
 360 LET H=60
 370 LET P=12
 395 SLOW
 400 PRINT " YOU HAVE JUST TELEPORTED ONTO  A DERELICT SPACE STATION. YOU   HAVE NO MONEY (THE CURENCY IS   ALTERIAN DOLLARS), AND YOUR","OBJECT THEREFORE IS TO TRAVEL   AROUND FROM ROOM TO ROOM,"
 410 PRINT "GAINING AS MUCH MONEY AS YOU CANWHILST TRYING TO STAY ALIVE. TO HELP YOU TO DO THIS YOU HAVE","WITH YOU A LASER PISTOL, A","SHIELD, A TORCH, THE HITCH-","HIKERS GUIDE TO THE GALAXY (HHG)AND A BABEL FISH, ENABLING YOU"
 420 PRINT "TO COMMUNICATE WITH MONSTERS.   YOU HAVE 20 BODY POINTS (PERFECTHEALTH), AND 200 ENERGY UNITS."
 430 PRINT " A ""*"" FLASHING IN THE BOTTOM   RIGHT CORNER OF THE SCREEN MEANSTYPE ANY LETTER TO CONTINUE."
 440 GOSUB 8000
 450 GOSUB 460
 455 GOTO 510
 460 CLS
 465 PRINT "THERE ARE 10 COMMANDS:-",,,"1 MOVE (ROOM TO ROOM)","2 ATTACK",,"3 FLEE",,"4 GET READY A DIFFERENT WEAPON","5 COMMUNICATE",,"6 SHIELD ON/OFF",,"7 USE A MEDIKIT",,"8 STATUS",,"9 SEARCH THE ROOM","10 LEAVE",,,,
 470 PRINT " YOUR WEAPON, TORCH AND SHIELD  ALL USE ENERGY. TO END THE GAME,RETURN TO THE TELEPORT ROOM AND EXECUTE COMMAND 10."
 480 GOTO 8000
 510 GOSUB 6000
 520 GOSUB 8000
 530 CLS
 540 PRINT "COMMAND (1 TO 10)?"
 550 INPUT C
 560 IF C<11 AND C>0 THEN GOTO 600
 570 PRINT ,,"1 TO 10 ONLY. WOULD YOU LIKE A  LIST OF THE LEGAL COMMANDS","(Y/N)?"
 580 INPUT X$
 590 IF X$="Y" THEN GOSUB 460
 595 GOTO 530
 600 CLS
 605 GOSUB 500+C*500
 610 GOTO 520
1000 REM move
1005 IF K=1 THEN PRINT "YOU CANT RISK TURNING YOUR BACK ON THE ";M$;"."
1007 IF K=1 THEN RETURN
1010 PRINT "WHICH WAY DO YOU WANT TO MOVE   (N/E/S/W)?"
1020 INPUT X$
1040 LET X=(1 AND X$="N")+(2 AND X$="E")+(3 AND X$="S")+(4 AND X$="W")
1080 IF X=0 THEN GOTO 1020
1090 IF R$(D,A,X)="1" THEN GOTO 1140
1100 PRINT ,,"THERE IS NO EXIT IN THE ";
1110 GOSUB 6400
1120 PRINT ,"WALL."
1130 RETURN
1140 LET D=D+(X=3)-(X=1)
1150 LET A=A+(X=2)-(X=4)
1160 GOTO 6000
1500 REM attack
1505 IF NOT K THEN PRINT "ATTACK? ATTACK WHO?"
1506 IF NOT K THEN RETURN
1507 IF P>E THEN PRINT "YOU DO NOT HAVE ENOUGH POWER TO ATTACK."
1508 IF P>E THEN RETURN
1510 PRINT "YOU ATTACK THE",,M$;"."
1515 LET E=E-P
1520 IF RND<H/100 THEN GOTO 1540
1530 PRINT ,,"YOU MISSED. BAD LUCK."
1535 GOTO 1585
1540 LET X=INT (RND*MD+1)
1550 PRINT ,,"YOU HIT, DOING ";X;" BODY POINTS OF"
1560 PRINT "DAMAGE."
1570 LET MBP=MBP-X
1580 IF MBP<=0 THEN GOTO 1610
1585 PRINT 
1590 GOTO 6500
1610 PRINT ,,"THE ";M$;" DIES."
1620 LET K=0
1625 LET R$(D,A,7)=" "
1630 PRINT ,,"DO YOU WISH TO SEARCH THE","DECEASED (Y/N)?"
1640 INPUT X$
1650 IF X$="N" THEN RETURN
1660 IF Z$="0" OR Z$="1" THEN GOTO 1920
1670 FOR I=1 TO LEN T$
1680 IF T$(I)=Z$ THEN GOTO 1740
1690 NEXT I
1700 PRINT ,,"YOU ADD ITS ";N$
1710 PRINT "TO YOUR ARMERY."
1720 LET T$=T$+Z$
1730 GOTO 1750
1740 PRINT ,,"YOU ALREADY HAVE A",N$;"."
1750 LET X=INT (RND*120+30)
1760 PRINT ,,"THE ";M$;" HAD"
1770 IF X>100 AND X<=200 THEN PRINT "AN UNUSED POWER PACK AND"
1775 IF X>200 THEN PRINT "TWO UNUSED POWER PACKS AND"
1780 PRINT "A PARTIALY USED POWER PACK,"
1790 LET E=E+X
1800 IF MI=1 THEN GOTO 1830
1810 PRINT "BUT IT WAS TOO UNINTELLIGENT TO CARRY ANYTHING ELSE."
1820 RETURN
1830 IF RND>.2 THEN GOTO 1860
1840 PRINT "A MEDIKIT, ";
1850 LET M=M+1
1860 IF RND>.7 THEN GOTO 1900
1870 LET X=INT (RND*200+50)
1880 PRINT "AND A$";X;"."
1890 LET AD=AD+X
1895 RETURN
1900 PRINT "BUT NO MONEY."
1910 RETURN
1920 PRINT ,,"THE ";M$;" WAS"
1930 PRINT "TOO UNINTELLIGENT TO CARRY","ANYTHING OF VALUE."
1940 RETURN
2000 REM flee
2010 IF K=1 THEN GOTO 2040
2020 PRINT "WHO ARE YOU GOING TO FLEE FROM, STUPID."
2030 RETURN
2040 IF RND>.1 THEN GOTO 2048
2042 PRINT "YOU TRY TO FLEE, BUT FALL OVER.",,,
2044 GOTO 6500
2048 LET X=INT (RND*4+1)
2050 IF R$(D,A,X)="0" THEN GOTO 2040
2060 PRINT "YOU FLEE THROUGH THE ";
2070 GOSUB 6400
2080 PRINT " WALL."
2090 GOSUB 6900
2100 LET Y=A+(X=2)-(X=4)
2110 LET X=D+(X=3)-(X=1)
2120 IF R$(X,Y,7)<>" " OR RND<.5 THEN GOTO 2200
2130 PRINT ,,"... UNFORTUNATELY THE",M$;" FOLLOWED"
2140 PRINT "YOU."
2150 GOSUB 6860
2200 LET D=X
2210 LET A=Y
2220 GOSUB 8000
2230 GOTO 6000
2500 REM weapon
2502 IF T$<>"2" THEN GOTO 2510
2505 PRINT "YOU ONLY HAVE ONE WEAPON."
2507 RETURN
2510 PRINT "YOUR PRESENT READY WEAPON IS A",W$;"."
2520 PRINT ,,"YOU HAVE THE FOLLOWING WEAPONS:-",,
2530 FOR I=1 TO LEN T$
2540 RAND 9310+VAL T$(I)*10
2550 RAND USR RESTORE
2560 PRINT I;". ";A$( TO USR READ)
2570 NEXT I
2580 PRINT ,,"WHICH WEAPON (1 TO ";LEN T$;")?"
2590 INPUT X
2600 IF X<1 OR X>LEN T$ THEN GOTO 2590
2610 RAND 9310+VAL T$(X)*10
2615 RAND USR RESTORE
2620 LET W$=A$( TO USR READ)
2630 LET MD=VAL A$( TO USR READ)
2640 LET H=VAL A$( TO USR READ)
2650 LET P=VAL A$( TO USR READ)
2655 IF NOT K THEN RETURN
2660 CLS
2665 RAND 
2670 GOTO 6500
3000 REM communicate
3010 IF K=1 THEN GOTO 3040
3020 PRINT "TALKING TO YOURSELF IS THE FIRSTSIGN OF MADNESS."
3030 RETURN
3040 IF MI=1 THEN GOTO 3070
3050 PRINT "THE ";M$;" IS TOO"
3055 PRINT "UNINTELLIGENT TO COMMUNICATE."
3060 RETURN
3070 PRINT "DO YOU WANT TO BRIBE IT,","THREATEN IT, OR BE FRIENDLY","TOWARDS IT (B/T/F)"
3080 INPUT X$
3090 IF X$="B" THEN GOTO 3130
3100 IF X$="T" THEN GOTO 3270
3110 IF X$="F" THEN GOTO 3330
3120 GOTO 3080
3130 PRINT ,,"HOW MANY DOLLARS DO YOU WANT TO OFFER IT?"
3140 INPUT X
3150 IF X<=AD THEN GOTO 3180
3160 PRINT ,,"IT CAN SEE THAT YOU HAVENT GOT  THAT MUCH MONEY.",,,
3170 GOTO 6500
3180 PRINT ,,"IT THINKS ABOUT IT ....."
3190 IF X>RND*1000 THEN GOTO 3220
3200 PRINT "AND DECIDES ITS A BAD IDEA.",,,
3210 GOTO 6500
3220 LET AD=AD-X
3225 PRINT "AND ACCEPTS. IT GOES OFF AND","LEAVES YOU IN PEACE."
3230 GOTO 6820
3270 IF RND<.2 THEN GOTO 3300
3280 PRINT ,,"IT DOESNT LIKE YOUR AGGRESIVE   ATTITUDE.",,,,
3290 GOTO 6500
3300 PRINT ,,"THE ";M$
3310 PRINT "SCURRIES AWAY WIMPERING."
3320 GOTO 6820
3330 IF RND<.1 THEN GOTO 3360
3340 PRINT ,,"IT DOESNT LIKE YOUR SMILE.",,,
3350 GOTO 6500
3360 PRINT ,,"THE ";M$;" THINKS"
3370 PRINT "YOUR AN O.K. GUY. IT GIVES YOU"
3380 LET X=INT (RND*250+1)
3390 PRINT "A$";X;", AND LEAVES."
3400 LET AD=AD+X
3410 GOTO 6820
3500 REM shield
3510 GOSUB 3700
3520 PRINT ,,"DO YOU WANT TO CHANGE THIS","(Y/N)?",,,,
3530 INPUT X$
3540 IF X$="Y" THEN LET S=NOT S
3700 PRINT "YOUR SHIELD IS ";"ON." AND S;"OFF." AND NOT S
3710 RETURN
4000 REM medikit
4010 IF M THEN GOTO 4040
4020 PRINT "YOU HAVE NO MEDIKITS LEFT."
4030 RETURN
4040 IF BP<20 THEN GOTO 4070
4050 PRINT "YOU FEEL FINE. YOU DONT NEED A  MEDIKIT."
4060 RETURN
4070 LET BP=BP+7+INT (RND*3+1)
4080 IF BP>20 THEN LET BP=20
4090 PRINT "YOU FEEL BETTER. YOU HAVE ";BP,"BODY POINTS.",,,,
4095 LET M=M-1
4100 GOTO 6500
4500 REM status
4510 PRINT TAB 10;"STATUS",,,
4520 PRINT "BODY POINTS >";BP,,,,"MEDIKITS >";M,,,,"POWER >";E,,,,"MONEY >A$";AD,,,,"WEAPONS >";LEN T$,,,,"READY WEAPON >";W$
4525 PRINT 
4530 GOTO 3700
5000 REM search
5002 IF K THEN PRINT "YOU CANT SEARCH THE ROOM WITH A",M$;" IN HERE."
5005 IF K THEN RETURN
5010 IF D/2=INT (D/2) AND A/2=INT (A/2) THEN GOTO 5140
5020 PRINT "IT IS VERY DARK IN HERE.",,,
5030 IF E<20 THEN GOTO 5080
5040 PRINT "DO YOU WANT TO SEARCH THE ROOM  IN THE DARK, USE YOUR TORCH OR  FORGET ABOUT IT (D/T/F)?"
5050 INPUT X$
5060 IF X$="D" OR X$="T" THEN GOTO 5160
5070 RETURN
5090 PRINT "UNFORTUNATELY YOU DO NOT HAVE   ENOUGH POWER TO USE YOUR TORCH. DO YOU STILL WANT TO SEARCH THE ROOM (Y/N)?"
5100 INPUT X$
5110 IF X$="N" THEN RETURN
5120 LET X$="D"
5130 GOTO 5160
5140 PRINT "YOU ARE LUCKY. IT IS LIGHT","ENOUGH TO SEARCH THE ROOM","WITHOUT A TORCH."
5150 LET X$="L"
5160 PRINT ,,"SEARCHING ..."
5170 FOR I=1 TO 100
5180 NEXT I
5190 IF X$="D" AND RND>.5 THEN GOSUB 7000
5200 PRINT ,,"YOU FOUND ... ";
5210 IF X$="T" THEN LET E=E-20
5220 LET X$=R$(D,A,6)
5230 LET R$(D,A,6)=" "
5240 IF X$<>"P" THEN GOTO 5320
5250 IF RND<.66 THEN GOTO 5282
5260 PRINT "A POWER PACK."
5270 LET E=E+100
5280 RETURN
5282 IF RND<.5 THEN GOTO 5290
5284 PRINT "TWO POWER PACKS."
5286 LET E=E+200
5288 RETURN
5290 PRINT "THREE POWER PACKS."
5300 LET E=E+300
5310 RETURN
5320 IF X$<>"M" THEN GOTO 5360
5330 PRINT "A MEDIKIT."
5340 LET M=M+1
5350 RETURN
5360 IF X$<>"D" THEN GOTO 5410
5370 LET X=INT (50+RND*RND*RND*2000)
5380 PRINT "A$";X;"."
5390 LET AD=AD+X
5400 RETURN
5410 PRINT "NOTHING."
5420 RETURN
5500 REM leave
5510 IF R$(D,A,5)="L" THEN GOTO 5540
5520 PRINT "YOU ARE NOT IN THE TELEPORT","ROOM."
5530 RETURN
5540 IF K THEN PRINT "YOU CANT USE THE TELEPORT WITH  A ";M$;" IN HERE."
5550 IF K THEN RETURN
5560 PRINT "YOU LEAVE VIA THE TELEPORT."
5570 GOTO 6680
6000 REM new#room
6002 LET ME=INT (RND*150+100)
6005 CLS
6010 PRINT "YOU HAVE ENTERED A NEW ROOM."
6020 RAND 9100+(CODE R$(D,A,5)-37)*10
6030 RAND USR RESTORE
6040 LET N$=A$( TO USR READ)
6050 PRINT ,,"ROOM TYPE >";N$
6060 PRINT ,,"EXITS >";
6070 FOR X=1 TO 4
6080 IF R$(D,A,X)="0" THEN GOTO 6105
6090 GOSUB 6400
6100 PRINT " ";
6105 NEXT X
6110 PRINT 
6120 IF R$(D,A,7)<>" " THEN GOTO 6150
6130 PRINT ,,"THERE IS NO ONE IN HERE."
6135 LET K=0
6140 RETURN
6150 PRINT ,,"THERE IS SOMEONE,"
6155 RAND 9500+(CODE R$(D,A,7)-37)*10
6157 RAND USR RESTORE
6160 LET M$=A$( TO USR READ)
6170 LET X$=A$( TO USR READ)
6180 LET MBP=VAL A$( TO USR READ)
6185 IF R$(D,A,8)<>" " THEN GOSUB 6330
6190 LET MI=VAL A$( TO USR READ)
6200 LET Z$=A$( TO USR READ)
6210 PRINT "... SOMETHING IN HERE."
6220 RAND 9310+VAL Z$*10
6225 RAND USR RESTORE
6230 LET N$=A$( TO USR READ)
6240 LET MMD=VAL A$( TO USR READ)
6250 LET MH=VAL A$( TO USR READ)
6255 LET MP=VAL A$( TO USR READ)
6260 PRINT ,,"THE HHG SAYS:-"
6270 PRINT " ""ITS A ";M$;","
6280 PRINT "ARMED WITH ";N$;"."
6290 PRINT X$;"."""
6310 LET K=1
6315 RAND 
6320 RETURN
6330 LET MBP=CODE R$(D,A,8)
6340 LET ME=CODE R$(D,A,9)
6360 RETURN
6400 REM direction
6410 IF X=1 THEN PRINT "NORTH";
6420 IF X=2 THEN PRINT "EAST";
6430 IF X=3 THEN PRINT "SOUTH";
6440 IF X=4 THEN PRINT "WEST";
6450 RETURN
6500 REM monster#attacks
6505 IF NOT K THEN RETURN
6507 IF ME<MP OR RND<.1 AND MI THEN GOTO 6800
6510 PRINT "THE ";M$
6520 PRINT "ATTACKS WITH ITS",N$;"."
6525 LET ME=ME-MP
6530 IF RND<MH/100 THEN GOTO 6570
6540 LET X=INT (RND*4+1)
6550 PRINT ,,"IT JUST MISSED YOUR ";"HEAD" AND X=1;"ARM" AND X=2;"CHEST" AND X=3;"SHOULDER" AND X=4;"."
6560 RETURN
6570 LET X=INT (RND*MMD+1)
6580 IF NOT S OR NOT E THEN GOTO 6620
6590 LET X=INT (X/2+.5)
6600 LET E=E-X*5
6610 IF E<0 THEN LET E=0
6615 IF E=0 THEN PRINT ,,"ENERGY DEPLETED."
6620 PRINT ,,"IT HIT, DOING ";X;" BODY POINTS OF","DAMAGE."
6630 LET BP=BP-X
6640 IF BP>0 THEN RETURN
6650 GOSUB 8000
6655 CLS
6660 PRINT "YOU WERE FATALY INJURED AND","DIED. THE ";M$
6670 PRINT "STOLE ALL YOUR POSESIONS AND","MUTILATED YOUR BODY."
6680 PRINT ,,"YOU HAD A$";AD;"."
6685 PRINT ,,"WOULD YOU LIKE ANOTHER GO (Y/N)?"
6690 INPUT X$
6700 IF X$="Y" THEN RUN 
6710 POKE 16389,128
6720 STOP
6800 PRINT "THE ";M$;" PANICS"
6801 IF RND>.33 THEN GOTO 6810
6802 PRINT "AND FALLS OVER IN AN ATTEMPT TO RUN AWAY."
6805 RETURN
6810 PRINT "AND RUNS AWAY."
6820 LET X$=""
6821 LET X=D
6822 LET Y=A
6824 LET Z=INT (RND*4+1)
6826 IF R$(X,Y,Z)="0" THEN GOTO 6824
6827 LET X$=X$+"1234"(Z)
6828 LET X=X+(Z=3)-(Z=1)
6830 LET Y=Y+(Z=2)-(Z=4)
6831 IF R$(X,Y,7)<>" " THEN GOTO 6824
6832 PRINT ,,"DO YOU WANT TO FOLLOW IT (Y/N)?"
6833 INPUT H$
6834 IF H$="N" THEN GOTO 6859
6835 CLS
6836 GOSUB 6859
6837 FOR I=1 TO LEN X?
6838 IF I=1 THEN GOTO 6841
6839 IF RND<.1 THEN GOTO 6853
6840 PRINT "THEN ";
6841 LET X=VAL X$(I)
6842 PRINT "YOU FOLLOW IT THROUGH THE",
6843 GOSUB 6400
6844 PRINT " WALL.",,,,
6845 IF I/7<>INT (I/7) OR I=LEN X$ THEN GOTO 6848
6846 GOSUB 8000
6847 CLS
6848 LET D=D+(X=3)-(X=1)
6849 LET A=A+(X=2)-(X=4)
6850 NEXT I
6851 GOSUB 8000
6852 GOTO 6000
6853 PRINT "YOU LOST IT AT THIS POINT."
6854 GOTO 6851
6859 GOSUB 6900
6860 LET R$(X,Y,7 TO )=R$(D,A,7 TO )
6870 LET R$(D,A,7)=" "
6875 LET K=0
6880 RETURN
6900 LET R$(D,A,8)=CHR$ MBP
6910 LET R$(D,A,9)=CHR$ ME
6920 RETURN
7000 LET X=INT (RND*5+1)
7010 IF X=1 THEN PRINT ,,"YOU STUMBLED IN THE DARK AND","STUBBED YOUR TOE."
7020 IF X=2 OR X=3 THEN PRINT ,,"YOU WALKED INTO A WALL IN THE   DARK."
7030 IF X>3 THEN PRINT ,,"YOU FELL OVER AND BROKE YOUR","LEG."
7040 PRINT ,,"YOU DID ";X;" BODY POINTS OF","DAMAGE."
7050 LET BP=BP-X
7060 IF BP>0 THEN RETURN
7070 PRINT ,"YOU DIED. BAD LUCK."
7080 GOTO 6680
8000 PRINT AT 20+2**0,31;"*"
8010 IF INKEY$ <>"" THEN RETURN
8020 PRINT AT 20+2**0,31;" "
8030 IF INKEY$ <>"" THEN RETURN
8040 GOTO 8000
9000 REM door#data
9010 REM ,0110,0101,0011,0110,0111,0111,0011,0010
9020 REM ,1110,0111,1101,1011,1110,1001,1110,1001
9030 REM ,1110,1001,0110,1111,1101,0111,1111,0011
9040 REM ,1110,0111,1111,1101,0111,1011,1100,1011
9050 REM ,1010,1010,1010,0110,1101,1111,0111,1001
9060 REM ,1110,1101,1111,1111,0111,1001,1110,0011
9070 REM ,1110,0111,1011,1110,1111,0111,1111,1001
9080 REM ,1100,1101,1101,1001,1100,1001,1100,0001
9100 REM room#name#data
9110 REM ,DETENTION CELL
9120 REM ,WEAPON STORES
9130 REM ,SICK BAY
9140 REM ,PERSONNEL QUARTERS
9150 REM ,WORKSHOP
9160 REM ,AUXILIARY CONTROL
9170 REM ,EMERGENCY POWER ROOM
9180 REM ,STORE ROOM
9190 REM ,TORTURE CELL
9200 REM ,WASTE DISPOSAL ROOM
9210 REM ,MASTER CONTROL ROOM
9220 REM ,TELEPORT ROOM
9300 REM weapons#data
9310 REM ,FANGS,2,25,0
9320 REM ,CLAWS,1,50,0
9330 REM ,LASER PISTOL,4,60,12
9340 REM ,LASER RIFLE,5,70,17
9350 REM ,LASER BLASTER,8,50,20
9360 REM ,KILL-O-ZAP GUN,20,20,20
9370 REM ,LIGHT SABRE,4,40,8
9380 REM ,SONIC GUN,4,45,8
9390 REM ,PLASMA PEA-SHOOTER,2,25,3
9400 REM ,NEUTRON BLADE,5,35,9
9500 REM monster#data
9510 REM ,VOGON GUARD,HARD TO KILL,18,1,2
9520 REM ,BETELGEUSIAN REBEL,BE CAREFUL,16,1,9
9530 REM ,VAMPIRE GLOB,UGLY BUT FEEBLE,11,0,0
9540 REM ,DAMOGRANIAN WARRIOZ,STAY AWAY,17,1,3
9550 REM ,MAGRATHEAN OUTLAW,COULD BE WORSE,10,1,9
9560 REM ,MUTANT DINGO,PATHETIC CREATURE,4,0,1
9570 REM ,SECURITY ROBOT MK I,SIMPLE,5,0,2
9580 REM ,SECURITY ROBOT MK II,NOT SO SIMPLE,10,0,3
9590 REM ,SECURITY ROBOT MK III,HIGHLY DANGEROUS,20,0,4
9600 REM ,RIGELIAN LORD,WATCH YOUR STEP,16,1,6
9610 REM ,DENTRASSI ZOMBIE,DONT WORRY ABOUT IT,8,0,7
9620 REM ,XYPER-SERPENT,NO TROUBLE,7,0,0
9630 REM ,KLAXON INTRUDER,KEEP COOL,13,1,7
9640 REM ,GALACTIC COP,USE YOUR SHIELD,12,1,5
9650 REM ,GOLGAFRINCHAN,SILLY GOOF,4,1,8
9660 REM ,FEDERAL HITMAN,RUN FOR YOUR LIFE,21,1,3
9670 REM ,BUGBLATTER BEAST,RAVENOUS,15,0,0
9680 REM ,ROMULAN TYRANT,SLOW TO DIE,18,1,6
9690 REM ,DEMIBRAIN MORON,HAVE A GOOD LAUGH,7,0,8
9700 REM ,FAULTY DROID,SPARKS WILL FLY,9,0,9
9710 REM end#of#data
9800 LPRINT "################################"
9810 GOTO 9800
9820 GOTO 9800
9996 SAVE "HITCH HIKEr"
9997 RUN 
9998 REM "HITCH HIKER"
9999 REM (C) SIMON LANE, 1982
